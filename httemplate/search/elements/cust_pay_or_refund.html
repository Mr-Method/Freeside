<%doc>

Examples:

  include( 'elements/cust_pay_or_refund.html',
               'thing'          => 'pay',
               'amount_field'   => 'paid',
               'name_singular'  => 'payment',
               'name_verb'      => 'paid',
         )

  include( 'elements/cust_pay_or_refund.html',
               'thing'          => 'refund',
               'amount_field'   => 'refund',
               'name_singular'  => 'refund',
               'name_verb'      => 'refunded',
         )

  include( 'elements/cust_pay_or_refund.html',
               'thing'          => 'pay_pending',
               'amount_field'   => 'paid',
               'name_singular'  => 'pending payment',
               'name_verb'      => 'pending',
               'disable_link'   => 1,
               'disable_by'     => 1,
               'html_init'      => '',
               'addl_header'    => [],
               'addl_fields'    => [],
               'addl_sort_fields' => [],
               'redirect_empty' => $redirect_empty,
          )

  include( 'elements/cust_pay_or_refund.html',
               'table'          => 'h_cust_pay',
               'amount_field'   => 'paid',
               'name_singular'  => 'payment',
               'name_verb'      => 'paid',
               'pre_header'     => [ 'Transaction',    'By' ],
               'pre_fields'     => [ 'history_action', 'history_user' ],
         )

</%doc>
<& grouped-search.html,
                'title'          => $title, # XXX: translate
                'name_singular'  => $name_singular,
                'query'          => $sql_query,
                'count_query'    => $count_query,
                'count_addl'     => \@count_addl,
                'redirect_empty' => $opt{'redirect_empty'},
                'header'         => \@header,
                'fields'         => \@fields,
                'sort_fields'    => \@sort_fields,
                'align'          => $align,
                'links'          => \@links,
                'link_onclicks'  => \@link_onclicks,
                'color'          => \@color,
                'style'          => \@style,

                'group_column'   => 'payby',
                'group_label'    => 'payby_name',
                'amount_field'   => $amount_field,
                'subtotal'       => { $opt{amount_field} => "sum($amount_field)" },
                'subtotal_row'   => [ 'Subtotal',
                                      sub { sprintf($money, $_[0]->$amount_field) },
                                    ],
                'total_row'      => [ '<B>Total</B>',
                                      sub { sprintf("<B>$money</B>", $_[0]->$amount_field) },
                                    ],
                'show_combined'  => 1,
&>
<%shared>
# canonicalize the payby subtype string to an SQL-quoted list
my %cardtype_of = (
  'VisaMC'    => q['VISA card', 'MasterCard'],
  'Amex'      => q['American Express card'],
  'Discover'  => q['Discover card'],
  'Maestro'   => q['Switch', 'Solo', 'Laser'],
  'Tokenized' => q['Tokenized'],
);  
</%shared>
<%init>

my %opt = @_;

my $curuser = $FS::CurrentUser::CurrentUser;

my $conf = FS::Conf->new;
my $money = ($conf->config('money_char') || '$') . '%.2f';

die "access denied"
  unless $curuser->access_right('Basic payment and refund reports');

my $table = $opt{'table'} || 'cust_'.$opt{'thing'};

my $has_reason = dbdef->table($table)->column('reasonnum') ? 1 : 0;

my $amount_field = $opt{'amount_field'};
my $name_singular = $opt{'name_singular'};

my $unapplied = $cgi->param('unapplied');
my $title = '';
$title = 'Unapplied ' if $unapplied;
$title .= "\u$name_singular Search Results";

###NOT USED???
#my $link = '';
#if (    ( $curuser->access_right('View invoices') #remove in 2.5 (2.7?)
#          || ($curuser->access_right('View payments') && $table =~ /^cust_pay/)
#          || ($curuser->access_right('View refunds') && $table eq 'cust_refund')
#        )
#     && ! $opt{'disable_link'}
#   )
#{
#
#  my $key;
#  my $q = '';
#  if ( $table eq 'cust_pay_void' ) {
#    $key = 'paynum';
#    $q .= 'void=1;';
#  } elsif ( $table eq /^cust_(\w+)$/ ) {
#    $key = $1.'num';
#  }
#  
#  if ( $key ) {
#    $q .= "$key=";
#    $link = [ "${p}view/$table.html?$q", $key ]
#  }
#}

my $cust_link = sub {
  my $cust_thing = shift;
  $cust_thing->cust_main_custnum
    ? [ "${p}view/cust_main.cgi?", 'custnum' ] 
    : '';
};

# only valid for $table == 'cust_pay' atm
my  $tax_names = '';
if ( $cgi->param('tax_names') ) {
  if ( dbh->{Driver}->{Name} =~ /^Pg/i ) {

    $tax_names = "
      array_to_string(
        array(
          SELECT itemdesc
            FROM cust_bill_pay
            LEFT JOIN cust_bill_pay_pkg USING ( billpaynum )
            LEFT JOIN cust_bill_pkg USING ( billpkgnum )
              WHERE cust_bill_pkg.pkgnum = 0
                AND cust_bill_pay.paynum = cust_pay.paynum
        ), '|'
      ) AS tax_names"
    ;

  } elsif ( dbh->{Driver}->{Name} =~ /^mysql/i ) {

    $tax_names = "GROUP_CONCAT(itemdesc SEPARATOR '|') AS tax_names";

  } else {

    warn "warning: unknown database type ". dbh->{Driver}->{Name}.
         "omitting tax name information from report.";

  }
}

my @header;
my @fields;
my @sort_fields;
my $align = '';
my @links;
my @link_onclicks;
if ( $opt{'pre_header'} ) {
  push @header, @{ $opt{'pre_header'} };
  $align .= 'c' x scalar(@{ $opt{'pre_header'} });
  push @links, map '', @{ $opt{'pre_header'} };
  push @fields, @{ $opt{'pre_fields'} };
  push @sort_fields, @{ $opt{'pre_fields'} };
}

my $sub_receipt = $opt{'disable_link'} ? '' : sub {
  my $obj = shift;
  my $objnum = $obj->primary_key . '=' . $obj->get($obj->primary_key);
  my $table = $obj->table;
  my $void = '';
  if ($table eq 'cust_pay_void') {
    $table = 'cust_pay';
    $void = ';void=1';
  }

  include('/elements/popup_link_onclick.html',
    'action'  => $p.'view/'.$table.'.html?link=popup;'.$objnum.$void,
    'actionlabel' => emt('Payment Receipt'),
  );
};

push @header, "\u$name_singular",
              'Amount',
;
$align .= 'rr';
push @links, '', '';
push @fields, 'payby_payinfo_pretty',
              sub { sprintf($money, shift->$amount_field() ) },
;
push @link_onclicks, $sub_receipt, '';
push @sort_fields, 'paysort', $amount_field;

# 4.x, to remain functional while the upgrade is running...
my $sub_guess_cardtype = sub {
  my $row = shift;
  $row->paycardtype || (
    ($row->payby eq 'CARD'  && $row->paymask !~ /N\/A/)
    ? cardtype($row->paymask)
    : ''
  )
};

if ($opt{'show_card_type'}) {
  push @header, emt('Card Type');
  $align .= 'r';
  push @links, '';
  push @fields, $sub_guess_cardtype;
  # worst case, paycardtype isn't filled in yet and sorting by that column
  # does nothing.
  push @sort_fields, 'paycardtype';
}

if ( $unapplied ) {
  push @header, emt('Unapplied');
  $align .= 'r';
  push @links, '';
  push @fields, sub { sprintf($money, shift->unapplied_amount) };
  push @sort_fields, '';
}

push @header, emt('Date');
$align .= 'r';
push @links, '';
push @fields, sub { time2str('%b %d %Y', shift->_date ) };
push @sort_fields, '_date';

if ($cgi->param('show_order_number')) {
  push @header, emt('Order Number');
  $align .= 'r';
  push @links, '';
  push @fields, 'order_number';
  push @sort_fields, 'order_number';
}

unless ( $opt{'disable_by'} ) {
  push @header, emt('By');
  $align .= 'c';
  push @links, '';
  push @fields, sub { my $o = shift->otaker;
                      $o = 'auto billing'          if $o eq 'fs_daily';
                      $o = 'customer self-service' if $o eq 'fs_selfservice';
                      $o;
                    };
  push @sort_fields, '';
}

if ( $tax_names ) {
  push @header, (emt('Tax names'), emt('Tax province'));
  $align .= 'cc';
  push @links, ('','');
  push @fields, sub { join (' + ', map { /^(.*?)(, \w\w)?$/; $1 }
                                   split('\|', shift->tax_names)
                           );
                    };
  push @fields, sub { join (' + ', map { if (/^(?:.*)(?:, )(\w\w)$/){ $1 }
                                         else { () }
                                       }
                                   split('\|', shift->tax_names)
                           );
                    };
  push @sort_fields, '', '';
}

push @header, FS::UI::Web::cust_header();
$align .=  FS::UI::Web::cust_aligns();
push @links, map { $_ ne 'Cust. Status' ? $cust_link : '' }
                 FS::UI::Web::cust_header();
my @color = ( ( map '', @fields ), FS::UI::Web::cust_colors() );
my @style = ( ( map '', @fields ), FS::UI::Web::cust_styles() );
push @fields, \&FS::UI::Web::cust_fields;
push @sort_fields, FS::UI::Web::cust_sort_fields;

push @header, @{ $opt{'addl_header'} }
  if $opt{'addl_header'};
push @fields, @{ $opt{'addl_fields'} }
  if $opt{'addl_fields'};
push @sort_fields, @{ $opt{'addl_sort_fields'} }
  if $opt{'addl_sort_fields'};

my( $count_query, $sql_query, @count_addl );
if ( $cgi->param('magic') ) {

  my @search = ();
  my @select = (
    "$table.*",
    "( $table.payby || ' ' || coalesce($table.paymask, $table.payinfo) ) AS paysort",
    FS::UI::Web::cust_sql_fields(),
    'cust_main.custnum AS cust_main_custnum',
  );
  push @select, $tax_names if $tax_names;

  my $orderby;
  if ( $cgi->param('magic') eq '_date' ) {

    if ( $cgi->param('agentnum') && $cgi->param('agentnum') =~ /^(\d+)$/ ) {
      push @search, "cust_main.agentnum = $1"; # $search{'agentnum'} = $1;
      my $agent = qsearchs('agent', { 'agentnum' => $1 } );
      die "unknown agentnum $1" unless $agent;
      $title = $agent->agent. " $title";
    }

    if ( $cgi->param('refnum') && $cgi->param('refnum') =~ /^(\d+)$/ ) {
      push @search, "cust_main.refnum = $1";
      my $part_referral = qsearchs('part_referral', { 'refnum' => $1 } );
      die "unknown refnum $1" unless $part_referral;
      $title = $part_referral->referral. " $title";
    }

    # cust_classnum - standard matching
    push @search, $m->comp('match-classnum',
        param => 'cust_classnum', field => 'cust_main.classnum'
      );

    if ( $cgi->param('custnum') =~ /^(\d+)$/ ) {
      push @search, "$table.custnum = $1";
    }

    if ( $cgi->param('payby') ) {

      my @all_payby_search = ();
      foreach my $payby_string ( $cgi->param('payby') ) {

        my $payby_search;

        my ($payby, $subtype) = split('-', $payby_string);
        # make sure it exists and is a transaction type
        if ( FS::payby->payment_payby2longname($payby) ) {
          $payby_search = "$table.payby = " . dbh->quote($payby);
        } else {
          die "illegal payby $payby_string";
        }

        if ( $subtype ) {

          my $in_cardtype = $cardtype_of{$subtype}
            or die "unknown card type $subtype";
          # will complete this phrase after alt_search
          $payby_search .= " AND ( $table.paycardtype IN($in_cardtype)";

          # 4.x transitional, to avoid breaking things while we upgrade
          my $similar_to = dbh->{Driver}->{Name} =~ /^mysql/i
                             ? 'REGEXP' #doesn't behave exactly the same, but
                                        #should work for our patterns
                             : 'SIMILAR TO';

          my $alt_search;
          if ( $subtype eq 'VisaMC' ) {

            #avoid posix regexes for portability
            $alt_search =
              # Visa
              " ( (     substring($table.payinfo from 1 for 1) = '4'     ".
              #   is not Switch
              "     AND substring($table.payinfo from 1 for 4) != '4936' ".
              "     AND substring($table.payinfo from 1 for 6)           ".
              "         NOT $similar_to '49030[2-9]'                        ".
              "     AND substring($table.payinfo from 1 for 6)           ".
              "         NOT $similar_to '49033[5-9]'                        ".
              "     AND substring($table.payinfo from 1 for 6)           ".
              "         NOT $similar_to '49110[1-2]'                        ".
              "     AND substring($table.payinfo from 1 for 6)           ".
              "         NOT $similar_to '49117[4-9]'                        ".
              "     AND substring($table.payinfo from 1 for 6)           ".
              "         NOT $similar_to '49118[1-2]'                        ".
              "   )".
              # MasterCard
              "   OR substring($table.payinfo from 1 for 2) = '51' ".
              "   OR substring($table.payinfo from 1 for 2) = '52' ".
              "   OR substring($table.payinfo from 1 for 2) = '53' ".
              "   OR substring($table.payinfo from 1 for 2) = '54' ".
              "   OR substring($table.payinfo from 1 for 2) = '54' ".
              "   OR substring($table.payinfo from 1 for 2) = '55' ".
              "   OR substring($table.payinfo from 1 for 4) $similar_to '222[1-9]' ".
              "   OR substring($table.payinfo from 1 for 3) $similar_to '22[3-9]' ".
              "   OR substring($table.payinfo from 1 for 2) $similar_to '2[3-6]' ".
              "   OR substring($table.payinfo from 1 for 3) $similar_to '27[0-1]' ".
              "   OR substring($table.payinfo from 1 for 4) = '2720' ".
              "   OR substring($table.payinfo from 1 for 3) = '2[2-7]x' ".
              " ) ";

          } elsif ( $subtype eq 'Amex' ) {

            $alt_search =
              " (    substring($table.payinfo from 1 for 2 ) = '34' ".
              "   OR substring($table.payinfo from 1 for 2 ) = '37' ".
              " ) ";

          } elsif ( $subtype eq 'Discover' ) {

            my $country = $conf->config('countrydefault') || 'US';

            $alt_search =
              " (    substring($table.payinfo from 1 for 4 ) = '6011'  ".
              "   OR substring($table.payinfo from 1 for 3 ) = '60x'   ".
              "   OR substring($table.payinfo from 1 for 2 ) = '65'    ".

              # diner's 300-305 / 3095
              "   OR substring($table.payinfo from 1 for 3 ) = '300'   ".
              "   OR substring($table.payinfo from 1 for 3 ) = '301'   ".
              "   OR substring($table.payinfo from 1 for 3 ) = '302'   ".
              "   OR substring($table.payinfo from 1 for 3 ) = '303'   ".
              "   OR substring($table.payinfo from 1 for 3 ) = '304'   ".
              "   OR substring($table.payinfo from 1 for 3 ) = '305'   ".
              "   OR substring($table.payinfo from 1 for 4 ) = '3095'  ".
              "   OR substring($table.payinfo from 1 for 3 ) = '30x'   ".

              # diner's 36, 38, 39
              "   OR substring($table.payinfo from 1 for 2 ) = '36'    ".
              "   OR substring($table.payinfo from 1 for 2 ) = '38'    ".
              "   OR substring($table.payinfo from 1 for 2 ) = '39'    ".

              "   OR substring($table.payinfo from 1 for 3 ) = '644'   ".
              "   OR substring($table.payinfo from 1 for 3 ) = '645'   ".
              "   OR substring($table.payinfo from 1 for 3 ) = '646'   ".
              "   OR substring($table.payinfo from 1 for 3 ) = '647'   ".
              "   OR substring($table.payinfo from 1 for 3 ) = '648'   ".
              "   OR substring($table.payinfo from 1 for 3 ) = '649'   ".
              "   OR substring($table.payinfo from 1 for 3 ) = '64x'   ".

              # JCB cards in the 3528-3589 range identified as Discover inside US & territories (NOT Canada)
              ( $country =~ /^(US|PR|VI|MP|PW|GU)$/
               ?" OR substring($table.payinfo from 1 for 4 ) = '3528'  ".
                " OR substring($table.payinfo from 1 for 4 ) = '3529'  ".
                " OR substring($table.payinfo from 1 for 3 ) = '353'   ".
                " OR substring($table.payinfo from 1 for 3 ) = '354'   ".
                " OR substring($table.payinfo from 1 for 3 ) = '355'   ".
                " OR substring($table.payinfo from 1 for 3 ) = '356'   ".
                " OR substring($table.payinfo from 1 for 3 ) = '357'   ".
                " OR substring($table.payinfo from 1 for 3 ) = '358'   ".
                " OR substring($table.payinfo from 1 for 3 ) = '35x'   "
               :""
              ).

              #China Union Pay processed as Discover in US, Mexico and Caribbean
              ( $country =~ /^(US|MX|AI|AG|AW|BS|BB|BM|BQ|VG|KY|CW|DM|DO|GD|GP|JM|MQ|MS|BL|KN|LC|VC|MF|SX|TT|TC)$/
               ?" OR substring($table.payinfo from 1 for 3 ) $similar_to '62[24-68x]'   "
               :""
              ).

              " ) ";

          } elsif ( $subtype eq 'Maestro' ) {

            $alt_search =
              " (    substring($table.payinfo from 1 for 2 ) = '63'     ".
              "   OR substring($table.payinfo from 1 for 2 ) = '67'     ".
              "   OR substring($table.payinfo from 1 for 6 ) = '564182' ".
              "   OR substring($table.payinfo from 1 for 4 ) = '4936'   ".
              "   OR substring($table.payinfo from 1 for 6 )            ".
              "      $similar_to '49030[2-9]'                             ".
              "   OR substring($table.payinfo from 1 for 6 )            ".
              "      $similar_to '49033[5-9]'                             ".
              "   OR substring($table.payinfo from 1 for 6 )            ".
              "      $similar_to '49110[1-2]'                             ".
              "   OR substring($table.payinfo from 1 for 6 )            ".
              "      $similar_to '49117[4-9]'                             ".
              "   OR substring($table.payinfo from 1 for 6 )            ".
              "      $similar_to '49118[1-2]'                             ".
              " ) ";

          } elsif ( $subtype eq 'Tokenized' ) {

              $alt_search = " substring($table.payinfo from 1 for 2 ) = '99' ";

          } else { # shouldn't happen if there's a $subtype

            $alt_search = 'TRUE';
 
          }

          # alt_search is already paren'd if it contains OR.
          # now make sure it works if they're encrypted.
          my $masksearch = $alt_search;
          $masksearch =~ s/$table.payinfo/$table.paymask/g;
          $alt_search = "( ($table.paymask IS NOT NULL AND $masksearch)
                          OR $alt_search )";

          # close paren here
          $payby_search .= " OR ( $table.paycardtype IS NULL AND $alt_search ) )";

        } # if $subtype

        push @all_payby_search, $payby_search;

      }

      push @search, ' ( '. join(' OR ', @all_payby_search). ' ) ' if @all_payby_search;

    }

    if ( $cgi->param('paymask') ) {
      $cgi->param('paymask') =~ /^\s*(\d+)\s*$/
        or die "illegal paymask ". $cgi->param('paymask');
      my $regexp = regexp_sql();
      push @search, "$table.paymask $regexp '$1\$'";
    } 

    if ( $cgi->param('payinfo') ) {
      $cgi->param('payinfo') =~ /^\s*(\d+)\s*$/
        or die "illegal payinfo ". $cgi->param('payinfo');
      my $regexp = regexp_sql();
      push @search, "$table.payinfo $regexp '^0*$1\$'";
    }

    if ( $cgi->param('ccpay') =~ /^([\w-:]+)$/ ) {
      # I think that's all the characters we need to allow.
      # To avoid confusion, this parameter searches both auth and order_number.
      push @search, "($table.auth LIKE '$1%') OR ($table.order_number LIKE '$1%')";
      push @fields, 'auth', 'order_number';
      push @header, 'Auth #', 'Transaction #';
      push @sort_fields, '', '';
      $align .= 'rr';

    }

    if ( $cgi->param('usernum') =~ /^(\d+)$/ ) {
      push @search, "$table.usernum = $1";
    }

    #for cust_pay_pending...  statusNOT=done
    if ( $cgi->param('statusNOT') =~ /^(\w+)$/ ) {
      push @search, "$table.status != '$1'";
    }

    my($beginning, $ending) = FS::UI::Web::parse_beginning_ending($cgi);

    push @search, "$table._date >= $beginning ",
                  "$table._date <= $ending";

    if ( $table eq 'cust_pay_void' ) {
      my($v_beginning, $v_ending) =
        FS::UI::Web::parse_beginning_ending($cgi, 'void');
      push @search, "$table.void_date >= $v_beginning ",
                    "$table.void_date <= $v_ending";
    }

    push @search, FS::UI::Web::parse_lt_gt($cgi, $amount_field, $table);

    $orderby = '_date';

  } elsif ( $cgi->param('magic') eq 'paybatch' ) {

    $cgi->param('paybatch') =~ /^([\w\/\:\-\.]+)$/
      or die "illegal paybatch: ". $cgi->param('paybatch');

    $orderby = "LOWER(company || ' ' || last || ' ' || first )";

  } elsif ( $cgi->param('magic') eq 'batchnum' ) {

    $cgi->param('batchnum') =~ /^(\d+)$/
      or die "illegal batchnum: ".$cgi->param('batchnum');

    push @search, "batchnum = $1";

    $orderby = "LOWER(company || ' ' || last || ' ' || first )";

  } else {
    die "unknown search magic: ". $cgi->param('magic');
  }

  if ( $cgi->param('paybatch') =~ /^([\w\/\:\-\.]+)$/ ) {
    push @search, "paybatch = '$1'";
  }

  #unapplied payment/refund
  if ( $unapplied ) {
    push @select, '(' . "FS::$table"->unapplied_sql . ') AS unapplied_amount';
    push @search, "FS::$table"->unapplied_sql . ' > 0';

  }

  #for the history search
  if ( $cgi->param('history_action') =~ /^([\w,]+)$/ ) {
    my @history_action = split(/,/, $1);
    push @search, 'history_action IN ('.
                    join(',', map "'$_'", @history_action ). ')';
  }

  if (    $cgi->param('history_date_beginning')
       || $cgi->param('history_date_ending')    ) {
      my($h_beginning, $h_ending) =
        FS::UI::Web::parse_beginning_ending($cgi, 'history_date');
      push @search, "history_date >= $h_beginning ",
                    "history_date <= $h_ending";
  }

  #here is the agent virtualization
  push @search, $curuser->agentnums_sql;

  my $addl_from = FS::UI::Web::join_cust_main($table);
  my $group_by = '';

  # reasons, for refunds and voided payments
  if ( $has_reason ) {
    push @select, "reason.reason";
    $addl_from .= " LEFT JOIN reason USING (reasonnum)\n";
    push @fields, 'reason';
    push @sort_fields, 'reason.reason';
    push @header, emt('Reason');
    if ( $cgi->param('reasonnum') =~ /^(\d+)$/ ) {
      push @search, "COALESCE(reasonnum, 0) = $1";
    }
  }

  #check for customer tags
  my @tags;
  foreach my $p ($cgi->param) {
    if ($p =~ /^tagnum(\d+)/ && $1) {
      $addl_from .= " LEFT JOIN cust_tag ON (cust_tag.custnum = cust_pay.custnum)" unless @tags;
      push @tags, $1;
    }
  }
  if (@tags) {
    my $tags = join(',',@tags);
    push @search, "cust_tag.tagnum in ($tags)";
  }

  if ( $cgi->param('tax_names') ) {
    if ( dbh->{Driver}->{Name} =~ /^Pg/i ) {

      0;#twiddle thumbs

    } elsif ( dbh->{Driver}->{Name} =~ /^mysql/i ) {

      $addl_from .= "LEFT JOIN cust_bill_pay USING ( paynum )
                     LEFT JOIN cust_bill_pay_pkg USING ( billpaynum )
                     LEFT JOIN cust_bill_pkg USING ( billpkgnum ) AS tax_names";
      $group_by  .= "GROUP BY $table.*,cust_main_custnum,".
                    FS::UI::Web::cust_sql_fields();
      push @search,
       "( cust_bill_pkg.pkgnum = 0 OR cust_bill_pkg.pkgnum is NULL )";

    } else {

      warn "warning: unknown database type ". dbh->{Driver}->{Name}.
           "omitting tax name information from report.";

    }
  }

  #customer location... total false laziness w/cust_main/Search.pm

  my $current = '';
  unless ( $cgi->param('location_history') ) {
    $current = '
      AND (    cust_location.locationnum IN ( cust_main.bill_locationnum,
                                              cust_main.ship_locationnum
                                            )
            OR cust_location.locationnum IN (
                 SELECT locationnum FROM cust_pkg
                  WHERE cust_pkg.custnum = cust_main.custnum
                    AND locationnum IS NOT NULL
                    AND '. FS::cust_pkg->ncancelled_recurring_sql.'
               )
          )';
  }


  ##
  # address
  ##
  if ( $cgi->param('address') ) {
    my @values = $cgi->param('address');
    my @orwhere;
    foreach (grep /\S/, @values) {
      my $address = dbh->quote('%'. lc($_). '%');
      push @orwhere,
        "LOWER(cust_location.address1) LIKE $address",
        "LOWER(cust_location.address2) LIKE $address";
    }
    if (@orwhere) {
      push @search, "EXISTS(
        SELECT 1 FROM cust_location 
        WHERE cust_location.custnum = cust_main.custnum
          AND (".join(' OR ',@orwhere).")
          $current
        )";
    }
  }

  ##
  # city
  ##
  if ( $cgi->param('city') =~ /\S/ ) {
    my $city = dbh->quote($cgi->param('city'));
    push @search, "EXISTS(
      SELECT 1 FROM cust_location
      WHERE cust_location.custnum = cust_main.custnum
        AND cust_location.city = $city
        $current
    )";
  }

  ##
  # county
  ##
  if ( $cgi->param('county') =~ /\S/ ) {
    my $county = dbh->quote($cgi->param('county'));
    push @search, "EXISTS(
      SELECT 1 FROM cust_location
      WHERE cust_location.custnum = cust_main.custnum
        AND cust_location.county = $county
        $current
    )";
  }

  ##
  # state
  ##
  if ( $cgi->param('state') =~ /\S/ ) {
    my $state = dbh->quote($cgi->param('state'));
    push @search, "EXISTS(
      SELECT 1 FROM cust_location
      WHERE cust_location.custnum = cust_main.custnum
        AND cust_location.state = $state
        $current
    )";
  }

  ##
  # zipcode
  ##
  if ( $cgi->param('zip') =~ /\S/ ) {
    my $zip = dbh->quote($cgi->param('zip') . '%');
    push @search, "EXISTS(
      SELECT 1 FROM cust_location
      WHERE cust_location.custnum = cust_main.custnum
        AND cust_location.zip LIKE $zip
        $current
    )";
  }

  ##
  # country
  ##
  if ( $cgi->param('country') =~ /^(\w\w)$/ ) {
    my $country = uc($1);
    push @search, "EXISTS(
      SELECT 1 FROM cust_location
      WHERE cust_location.custnum = cust_main.custnum
        AND cust_location.country = '$country'
        $current
    )";
  }

  #end of false laziness w/cust_main/Search.pm

  my $search = ' WHERE '. join(' AND ', @search);

  $count_query = "SELECT COUNT(*), SUM($table.$amount_field) ";
  $count_query .= ', SUM(' . "FS::$table"->unapplied_sql . ') ' 
    if $unapplied;
  $count_query .= "FROM $table $addl_from".
                  "$search $group_by";

  @count_addl = ( '$%.2f total '.$opt{name_verb} );
  push @count_addl, '$%.2f unapplied' if $unapplied;

  $sql_query = {
    'table'     => $table,
    'select'    => join(', ', @select),
    'hashref'   => {},
    'extra_sql' => "$search $group_by",
    'order_by'  => "ORDER BY $orderby",
    'addl_from' => $addl_from,
  };

} else {

  #hmm... is this still used?
  warn "undefined search magic";

  $cgi->param('payinfo') =~ /^\s*(\d+)\s*$/ or die "illegal payinfo";
  my $payinfo = $1;

  $cgi->param('payby') =~ /^(\w+)$/ or die "illegal payby";
  my $payby = $1;

  $count_query = "SELECT COUNT(*), SUM($table.$amount_field) FROM $table".
                 "  WHERE payinfo = '$payinfo' AND payby = '$payby'".
                 "  AND ". $curuser->agentnums_sql;
  @count_addl = ( '$%.2f total '.$opt{name_verb} );

  $sql_query = {
    'table'     => $table,
    'hashref'   => { 'payinfo' => $payinfo,
                     'payby'   => $payby    },
    'extra_sql' => $curuser->agentnums_sql.
                   " ORDER BY _date",
  };

}

# for consistency
$title = join('',map {ucfirst} split(/\b/,$title));

</%init>
